# setup the gate template and its local declaration

set Gate = {}
set Gate.declaration = 'id_t list[N+1];
int[0,N] len;

// Put an element at the end of the queue
void enqueue(id_t element)
{
        list[len++] = element;
}

// Remove the front element of the queue
void dequeue()
{
        int i = 0;
        len -= 1;
        while (i < len)
        {
                list[i] = list[i + 1];
                i++;
        }
        list[i] = 0;
}

// Returns the front element of the queue
id_t front()
{
   return list[0];
}

// Returns the last element of the queue
id_t tail()
{
   return list[len - 1];
}
'

# setup the locations of the gate template

Gate(L0) = {}
Gate(Occ) = {}
Gate(Free) = {}
set Gate.committed = L0
set Gate.init = Free

# setup the edges of the gate template

Gate (Occ -> L0) = { select: 'e : id_t'; sync: 'appr[e]?'; assignment: 'enqueue(e)'; }
Gate (Occ -> Free) = { select: 'e : id_t'; guard: 'e == front()'; sync: 'leave[e]?'; assignment: 'dequeue()'; }
Gate (L0 -> Occ) = { sync: 'stop[tail()]!'; }
Gate(Free -> Occ) = { guard: 'len > 0'; sync: 'go[front()]!'; }
Gate (Free -> Occ) = { select: 'e : id_t'; guard: 'len == 0'; sync: 'appr[e]?'; assignment: 'enqueue(e)'; }